
    model{

        for(i in 1:N){
        # Likelihood
          Y[i] ~ dnorm(L[i], tau[Ti[i]])
          L[i] <- (w[i]/K)*(1-exp(-K*(Ti[i]-to)))

        # Linear predictor of w
          log(w[i]) <- beta0
        }

      # Priors on VBGF parameters (w defined below by linear model)
        # Brody growth coefficient
          K ~ dunif(0, 1)
        # Age at length zero
          to ~ dunif(-10, 1)

      # Priors on parameters of linear model on w
        # Intercept
          beta0 ~ dnorm(0, 0.001)

      # Prior distribution for precision at each age
      # This imposes a multiplicative error structure on length at age
        for(t in 1:Tmax){
          tau[t] ~ dgamma(0.01, 0.001)
        }
    }
